plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.22.3'
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base']
}

mainClassName = "app.main/cc.cc1234.PrettyZooApplication"

run {
    jvmArgs = ['-Djdk.gtk.version=2'] // required due to a bug in Java: https://github.com/javafxports/openjdk-jfx/issues/175
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    forceMerge("jackson", "log4j-api")
    launcher {
        name = 'prettyZoo'
        jvmArgs = ['-Djdk.gtk.version=2']
    }
    jpackage {
        def os = org.gradle.internal.os.OperatingSystem.current()
        installerOptions = [
                '--name', 'PrettyZoo',
                '--app-version', "${project.version}",
                '--description', 'Pretty nice Zookeeper GUI',
                '--copyright', 'Copyrigth',
                '--vendor', 'vran'
        ]
        if (os.windows) {
            installerType = 'msi'
            icon = 'src/main/resources/assets/icon/icon.ico'
            installerOptions += [
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-shortcut'
            ]
        }

        if (os.macOsX) {
            installerType = 'dmg'
            icon = 'src/main/resources/assets/icon/Icon.icns'
        }

        if (os.linux) {
            installerType = 'rpm'
            icon = 'src/main/resources/assets/icon/icon.png'
            installerOptions += [
                    '--linux-rpm-license-type', 'GPLv3',
                    '--linux-shortcut'
            ]
        }
    }
}

jpackage {
    doLast {
        file("${buildDir}/jpackage/prettyZoo-${version}.dmg").renameTo("${buildDir}/jpackage/prettyZoo-mac.dmg")
        file("${buildDir}/jpackage/prettyZoo-${version}.msi").renameTo("${buildDir}/jpackage/prettyZoo-win.msi")
        file("${buildDir}/jpackage/prettyZoo-${version}.rpm").renameTo("${buildDir}/jpackage/prettyZoo-linux.rpm")
    }
}

dependencies {
    compile project(':domain')
    compile project(':spi')
    testCompile group: 'junit', name: 'junit', version: '4.12'
//    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.2'
}

configJavafxRun {
    dependsOn classes
}